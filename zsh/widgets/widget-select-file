#!/bin/zsh

# Selects files or directories from the current directory.

__widget-select-file() {
  setopt pipe_fail
  local fzf_options=(--height 40% --reverse --multi)
  # The steps:
  #   1. Find all files, directories and symlinks.
  #   2. Chop off the first 2 bytes, i.e. the './' for each file.
  #   3. Select with FZF
  #   4. Print what we selected with spaces not newlines.
  find -L . -mindepth 1 \
       ${FZF_FIND_PRUNE_ARRAY[@]} \
       -o -type f -print \
       -o -type d -print \
       -o -type l -print 2> /dev/null \
    | cut --bytes 3- \
    | $FZF_COMMAND $fzf_options[@] $@ \
    | while read item; do echo -n "${(q)item} "; done
  local ret=$?
  echo
  return $ret
}

function widget-select-file() {
  LBUFFER=${LBUFFER}$(__widget-select-file)
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

widget-select-file
