#!/bin/zsh

function __widget-select-command() {
  # $functions is an associative array of function names to definitions.
  # k: expand only keys, i.e. function names
  # j: join with the null byte (\0).
  # #_*: filter function names that begin with an underscore
  local shell_functions=${(kj:\0:)functions#_}
  local external_commands=${(kj:\0:)commands}
  local -a fzf_options
  fzf_options=(--read0 --reverse --multi --header=Commands)
  print $shell_functions\0$external_commands \
    | fzf ${fzf_options[@]} \
    | while read item; do echo -n "${item} "; done
  local return_value=$?
  print
  return $return_value
}

function widget-select-command() {
  local spacing=''
  if [[ $KEYMAP == 'vicmd' && ${LBUFFER[-1]} != ' ' && -n $LBUFFER ]]; then
    spacing=' '
  fi
  LBUFFER="${LBUFFER}$spacing$(__widget-select-command)"
  local ret=$?
  zle redisplay
  zle zle-line-init
  return $ret
}

 widget-select-command
