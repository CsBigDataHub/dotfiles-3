#!/bin/zsh

function __widget-select-directory() {
  setopt pipe_fail
  local -a fzf_options
  fzf_options=(--reverse --multi)
  # The steps:
  #   1. Find all files, directories and symlinks.
  #   2. Chop off the first 2 bytes, i.e. the './' for each file.
  #   3. Select with FZF
  #   4. Print what we selected with spaces not newlines.
  find -L . -mindepth 1 \
    ${FZF_FIND_PRUNE_ARRAY[@]} \
    -o -type d -print \
    -o -type l -print 2> /dev/null \
    | cut -c 3- \
    | $FZF_COMMAND $fzf_options[@] $@ \
    | while read item; do echo -n "${(q)item} "; done
  local ret=$?
  echo
  return $ret
}

function widget-select-directory() {
  local spacing=''
  if [[ $KEYMAP == 'vicmd' && ${LBUFFER[-1]} != ' ' && -n $LBUFFER ]]; then
     spacing=' '
  fi
  LBUFFER=${LBUFFER}$spacing$(__widget-select-directory)
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

widget-select-directory
