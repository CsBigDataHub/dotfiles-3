#!/bin/zsh

# Selects files or directories from the current directory.

function __widget-select-bazel-package() {
  setopt pipe_fail
  local fzf_options=(--height 40% --reverse --multi)
  # The steps:
  #   1. Find all directories containing a BUILD file.
  #   2. Chop off the first 2 bytes, i.e. the './' for each file.
  #   3. Select with FZF
  #   4. Print what we selected with spaces not newlines.
  find -L . -mindepth 1 \
       ${FZF_FIND_PRUNE_ARRAY[@]} \
       -or \( -type d -and -exec test -f {}/BUILD \; \) \
       -and -print \
       2> /dev/null \
    | cut --bytes 3- \
    | $FZF_COMMAND ${fzf_options[@]} $@ \
    | while read item; do echo -n "${(q)item} "; done
  local ret=$?
  echo
  return $ret
}

function widget-select-bazel-package() {
  LBUFFER=${LBUFFER}$(__widget-select-bazel-package)
  local ret=$?
  zle redisplay
  typeset -f zle-line-init >/dev/null && zle zle-line-init
  return $ret
}

widget-select-bazel-package
